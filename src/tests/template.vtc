varnishtest "Notice two hit transactions"

# One VTC to test the four extension mechanisms

server s1 {
	rxreq
	txresp
} -start

varnish v1 -vcl+backend {
	import template;

	include "varnish-template/common.vcl";

	sub vcl_hit {
		call notice_transaction;
	}

	sub vcl_backend_fetch {
		call notice_transaction;
	}
} -start

process p1 {
	exec varnishtemplate -n ${v1_name}
} -start

# Don't start the first transaction too soon
delay 1

client c1 -repeat 3 {
	txreq
	rxresp
} -run

# Don't stop varnishtemplate too soon
delay 1

process p1 -stop

shell {
	set -e

	# the result should be the same in -d mode
	varnishtemplate -n ${v1_name} -d >shell.out
	diff -u shell.out ${p1_out}

	# varnishtemplate should have noticed 3 transactions
	n_trans=$(cat ${p1_out})
	test "$n_trans" = 3
}

varnish v1 -expect TPL.client == 2
varnish v1 -expect TPL.backend == 1
