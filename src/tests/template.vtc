varnishtest "Notice two hit transactions"

# One VTC to test the three extension mechanisms

server s1 {
	rxreq
	txresp
} -start

varnish v1 -vcl+backend {
	import template;

	include "varnish-template/common.vcl";

	sub vcl_hit {
		call notice_transaction;
	}
} -start

logexpect l1 -v v1 {
	expect 1004 * VCL_Log "varnish-template .* was here"
	expect 1006 * VCL_Log "varnish-template .* was here"
} -start

process p1 {
	exec varnishtemplate -n ${v1_name}
} -start

# Don't start the first transaction too soon
delay 1

client c1 -repeat 3 {
	txreq
	rxresp
} -run

# Don't stop varnishtemplate too soon
delay 1

logexpect l1 -wait
process p1 -stop

shell {
	set -e

	# the result should be the same in -d mode
	varnishtemplate -n ${v1_name} -d >shell.out
	diff -u shell.out ${p1_out}

	# varnishtemplate should have noticed 2 transactions
	n_trans=$(cat ${p1_out})
	test "$n_trans" = 2
}
